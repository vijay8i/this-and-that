# Declare build_type argument and default to "debug"
declare_args() {
  # Set the default build type to "debug"
  build_type = "debug"
}

# All binary targets will get this list of configs by default.
# Targets can out out of a config by removing from their local
# configs list.
shared_binary_target_configs = [
  "//build:compiler_defaults",
  "//build:bleeding-edge",
]

# Apply build type specific config
if (build_type == "debug") {
  shared_binary_target_configs += [ "//build:debug_config" ]
} else if (build_type == "release") {
  shared_binary_target_configs += [ "//build:release_config" ]
}

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = shared_binary_target_configs
  configs += [ "//build:executable_ldconfig" ]
}

set_defaults("static_library") {
  configs = shared_binary_target_configs
}

set_defaults("shared_library") {
  configs = shared_binary_target_configs
}

set_defaults("source_set") {
  configs = shared_binary_target_configs
}

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

is_linux = host_os == "linux" && current_os == "linux" && target_os == "linux"
is_mac = host_os == "mac" && current_os == "mac" && target_os == "mac"

set_default_toolchain("//build/toolchain:clang")
